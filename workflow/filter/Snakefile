from pathlib import Path
import pandas as pd

from utils.ModuleConfig import ModuleConfig
from utils.environments import get_env


mcfg = ModuleConfig(
    module_name='filter',
    config=config,
    config_params=[
        'dask',
        'subset',
        'write_copy',
        'remove_by_column',
        # 'keep_by_column',
        'remove_by_query',
        'keep_by_query',
    ],
)
params = mcfg.get_paramspace()

wildcard_constraints:
    dataset=r'[^/]+',
    file_id=r'[^/]+',

module common:
    snakefile: "../common/Snakefile"
    config: config

use rule * from common as common_ *

module filtering:
    snakefile: "rules/rules.smk"
    config: config


use rule filter from filtering as filter with:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / f'{params.wildcard_pattern}.zarr'),
    params:
        remove_by_column=lambda wildcards: mcfg.get_from_parameters(wildcards, 'remove_by_column', default={}),
        remove_by_query=lambda wildcards: mcfg.get_from_parameters(wildcards, 'remove_by_query', default={}),
        keep_by_query=lambda wildcards: mcfg.get_from_parameters(wildcards, 'keep_by_query', default={}),
        dask=lambda wildcards: mcfg.get_from_parameters(wildcards, 'dask', default=True),
        subset=lambda wildcards: mcfg.get_from_parameters(wildcards, 'subset', default=True),
        write_copy=lambda wildcards: mcfg.get_from_parameters(wildcards, 'write_copy', default=False),
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb',attempt=attempt, factor=2),


rule all:
    input:
        mcfg.get_output_files(rules.filter.output)
    default_target: True
    localrule: True


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=mcfg.image_dir,
            target='all'
        )
    localrule: True
